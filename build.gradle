import java.text.SimpleDateFormat

buildscript {
    apply from: "$rootDir/dependencies.gradle"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath gradlePlugins.kotlin
    }
}

static def buildTimeStamp() {
    return new SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
}

allprojects {
    defaultTasks 'clean', 'build', 'jacocoTestReport'
}

subprojects {
    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = "org.acmebank.katas.bankaccount"

    repositories {
        mavenCentral()
    }

    jar{
        archiveVersion = "${buildTimeStamp()}"
    }

    test {
        useJUnitPlatform()

        finalizedBy jacocoTestReport

        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) {
                    new File(layout.buildDirectory.get().asFile, "/jacoco/test-results.json").text =
                            "{\n" +
                                    "\"result\": \"${result.resultType}\", \n" +
                                    "\"tests\": \"${result.testCount}\", \n" +
                                    "\"passed\": \"${result.successfulTestCount}\", \n" +
                                    "\"failed\": \"${result.failedTestCount}\", \n" +
                                    "\"skipped\": \"${result.skippedTestCount}\"\n" +
                                    "}"
                }

            }
        }
    }
}
