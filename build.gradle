import java.text.SimpleDateFormat

buildscript {
    apply from: "$rootDir/dependencies.gradle"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath gradlePlugins.kotlin,
                gradlePlugins.pitest
    }
}

static def buildTimeStamp() {
    return new SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
}

allprojects {
    defaultTasks 'clean', 'build', 'pitest'
}

subprojects {
    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: "info.solidsoft.pitest"

    group = "org.acmebank.katas.bankaccount"

    repositories {
        mavenCentral()
    }

    jar {
        archiveVersion = "${buildTimeStamp()}"
    }

    pitest{
        threads = 4
        junit5PluginVersion = '1.0.0'
        failWhenNoMutations = false
    }

    test {
        useJUnitPlatform()

        finalizedBy jacocoTestReport

        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) {
                    new File(layout.buildDirectory.get().asFile, "/jacoco/test-results.json").text =
                            "{\n" +
                                    "\"result\": \"${result.resultType}\", \n" +
                                    "\"tests\": \"${result.testCount}\", \n" +
                                    "\"passed\": \"${result.successfulTestCount}\", \n" +
                                    "\"failed\": \"${result.failedTestCount}\", \n" +
                                    "\"skipped\": \"${result.skippedTestCount}\"\n" +
                                    "}"
                }

            }
        }
    }
}
