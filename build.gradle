import java.text.SimpleDateFormat

buildscript {
    apply from: "$rootDir/dependencies.gradle"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath gradlePlugins.kotlin,
                gradlePlugins.pitest
    }
}

static def gitSha() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (ignored) {
        return "unknown"
    }
}

static def gitBranch() {
    try {
        return 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    } catch (ignored) {
        return "unknown"
    }
}

static def buildTimeStamp() {
    return new SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
}

allprojects {
    defaultTasks 'clean', 'build', 'pitest'
}

subprojects {
    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: "info.solidsoft.pitest"

    group = "org.xpdojo"

    repositories {
        mavenCentral()
    }

    jar {
        archiveVersion = "${gitBranch()}-${gitSha()}-${buildTimeStamp()}"
    }

    pitest{
        threads = 4
        junit5PluginVersion = '1.2.1'
        failWhenNoMutations = false
        excludedMethods = ['toString', 'hashCode', 'equals']
    }

    test {
        useJUnitPlatform()

        finalizedBy jacocoTestReport

        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) {
                    new File(layout.buildDirectory.get().asFile, "/jacoco/test-results.json").text =
                            "{\n" +
                                    "\"result\": \"${result.resultType}\", \n" +
                                    "\"tests\": \"${result.testCount}\", \n" +
                                    "\"passed\": \"${result.successfulTestCount}\", \n" +
                                    "\"failed\": \"${result.failedTestCount}\", \n" +
                                    "\"skipped\": \"${result.skippedTestCount}\"\n" +
                                    "}"
                }

            }
        }
    }
}
